# Edit this configuration file to define what should be installed on
# your system.  Help is available in the configuration.nix(5) man page
# and in the NixOS manual (accessible by running ‘nixos-help’).

{ config, pkgs, ... }:

{
  # inotify settings to help reduce inotify watch limit errors
  boot.kernel.sysctl = {
    "fs.inotify.max_user_instances" = 4096;
    "fs.inotify.max_user_watches" = 524288;
  };

  programs.appimage.enable = true;
  programs.appimage.binfmt = true;

  # fix pinentry-gnome3
  services.dbus.packages = [ pkgs.gcr ];

  fileSystems."/mnt/FrozenVault" = {
    device = "//192.168.68.103/frozenvault1";
    fsType = "cifs";
    options =
      let
        # this line prevents hanging on network split
        automount_opts = "x-systemd.automount,noauto,x-systemd.idle-timeout=60,x-systemd.device-timeout=4s,x-systemd.mount-timeout=4s,nofail";

      in
      [
        "${automount_opts},file_mode=0777,dir_mode=0777,credentials=/home/sam/.config/home-manager/secrets/smb-credentials.txt"
      ];
  };

  nix.settings.experimental-features = [
    "nix-command"
    "flakes"
  ];

  hardware.bluetooth.enable = true; # enables support for Bluetooth
  hardware.bluetooth.powerOnBoot = true; # powers up the default Bluetooth controller on boot

  time.timeZone = "Europe/London";
  i18n.defaultLocale = "en_GB.UTF-8";

  i18n.extraLocaleSettings = {
    LC_ADDRESS = "en_GB.UTF-8";
    LC_IDENTIFICATION = "en_GB.UTF-8";
    LC_MEASUREMENT = "en_GB.UTF-8";
    LC_MONETARY = "en_GB.UTF-8";
    LC_NAME = "en_GB.UTF-8";
    LC_NUMERIC = "en_GB.UTF-8";
    LC_PAPER = "en_GB.UTF-8";
    LC_TELEPHONE = "en_GB.UTF-8";
    LC_TIME = "en_GB.UTF-8";
  };

  environment.enableDebugInfo = true;

  nixpkgs.config.allowUnfree = true;
  services.gvfs.enable = true;

  environment.systemPackages = with pkgs; [
    cifs-utils
    neovim
    pinentry-tty
  ];

  fonts.packages = [
    pkgs.nerd-fonts.jetbrains-mono
    pkgs.nerd-fonts.ubuntu
    pkgs.league-of-moveable-type
    pkgs.roboto
    pkgs.inter
    pkgs.quicksand
    (pkgs.noto-fonts.override {
      variants = [
        "NotoSans"
        "NotoSerif"
        "NotoMusic"
        "NotoSansSymbols"
        "NotoSansMath"
        "NotoSansMono"
      ];
    })
    pkgs.noto-fonts-emoji
    pkgs.barlow
    (pkgs.stdenvNoCC.mkDerivation rec {
      pname = "outfit-fonts";
      version = "1.1";

      src = pkgs.fetchzip {
        url = "https://github.com/Outfitio/Outfit-Fonts/archive/refs/tags/${version}.zip";
        hash = "sha256-d12SnIhD5LdrgZYH7zzQ8otnRyp45VTCC9vEXVsVKLY=";
      };

      installPhase = ''
        runHook preInstall
        install -Dm644 fonts/variable/*.ttf fonts/ttf/*.ttf -t $out/share/fonts/opentype
        runHook postInstall
      '';

      meta = with pkgs.lib; {
        description = "Outfit Fonts";
        homepage = "https://github.com/Outfitio/Outfit-Fonts";
        license = licenses.ofl;
        maintainers = [ ];
        platforms = platforms.all;
      };
    })
  ];

  # This value determines the NixOS release from which the default
  # settings for stateful data, like file locations and database versions
  # on your system were taken. It‘s perfectly fine and recommended to leave
  # this value at the release version of the first install of this system.
  # Before changing this value read the documentation for this option
  # (e.g. man configuration.nix or on https://nixos.org/nixos/options.html).
  system.stateVersion = "23.11"; # Did you read the comment?
}
